<html>
<head>

  <meta name='viewport' content='width=device-width, initial-scale=1.0, minimum-scale=1.0, maximum-scale=1.0' />

  <link rel="stylesheet" type="text/css" href="css/dojo.css">
  <link rel="stylesheet" type="text/css" href="css/opentip.css">
  <link rel="stylesheet" type="text/css" href="css/dragresize.css">

  <script type='text/javascript' src='lib/jquery.min.js'></script>
  <script type='text/javascript' src='lib/jquery-ui.min.js'></script>
  <script type='text/javascript' src='lib/jquery.slimscroll.min.js'></script>
  <script type="text/javascript" src='lib/zlib.min.js'></script>
  <script type="text/javascript" src='lib/xtk.js'></script>
  <script type="text/javascript" src='lib/opentip-native.min.js'></script>
  <script type="text/javascript" src='lib/dragresize.js'></script>

  <script type='text/javascript' src='dojo.js'></script>
  <script type='text/javascript' src='viewer.js'></script>
  <script type='text/javascript' src='controller.js'></script>
  <script type='text/javascript' src='camera.js'></script>
  <script type='text/javascript' src='interactor.js'></script>
  <script type='text/javascript' src='loader.js'></script>
  <script type='text/javascript' src='websocket.js'></script>
  <script type='text/javascript' src='offscreen_renderer.js'></script>
  <script type='text/javascript' src='util.js'></script>


<!--   // <script type="text/javascript" src="lib/X/lib/google-closure-library/closure/goog/base.js"></script>
  // <script type="text/javascript" src="lib/X/xtk-deps.js"></script> -->


  <!-- SHADERS -->
  <script id="fs1" type="x-shader/x-fragment">
    precision mediump float;
    precision mediump int;

    uniform sampler2D uImageSampler;
    uniform sampler2D uTextureSampler;
    uniform sampler2D uColorMapSampler;
    uniform sampler2D uMergeTableKeySampler;
    uniform sampler2D uMergeTableValueSampler;
    uniform sampler2D uLockTableSampler;
    uniform float uOpacity;
    uniform float uHighlightedId;
    uniform float uActivatedId;
    uniform int uSplitMode;
    uniform int uAdjustMode;
    uniform float uMaxColors;
    uniform int uBorders;
    uniform bool uOnlyLocked;
    uniform int uMergeTableEnd;
    uniform int uMergeTableLength;
    uniform int uLockTableLength;
    uniform bool uShowOverlay;

    const int MAX = 10000;
    const int bMAX = int(ceil(log2(float(MAX))));

    varying vec3 vPosition;
    varying vec2 vTexturePosition;

    //
    // 32 BIT <-> 8 BIT CONVERSIONS
    //

    // convert rgba to a 32 bit value
    float unpack (vec4 value) {
      // note: little endian
      const vec4 bitShifts = vec4( (256.0 * 256.0 * 256.0), (256.0 * 256.0), 256.0, 1);
      return dot(value.abgr , bitShifts);

    }

    // convert 32 bit int to rgba
    ivec4 pack (int value) {

      ivec4 o = ivec4(0., 0., 0., 0.);

      o.w = (value / (256*256*256));
      o.z = ((value-o.w) / (256*256));
      o.y = ((value-o.w-o.z) / (256));
      o.x = (value-o.y*256-o.z*256*256-o.w*256*256*256);

      return o;

    }

    //
    // FLOAT COMPARE FUNCTIONS WITH DELTA
    //
    bool equals(float id1, float id2) {
      return (abs(id1-id2) <= 0.2);
    }

    bool larger(float id1, float id2) {

      if (equals(id1, id2)) return false;

      return id1 > id2;
    }

    //
    // ID LOOKUPS
    //
    float lookup_key_(float m) {

      // Key for given index
      vec4 m_key = texture2D(uMergeTableKeySampler, vec2(m, 0.));
      return unpack(m_key)*255.;

    }
    float lookup_value_(float m) {

      // Value for given index
      vec4 m_value = texture2D(uMergeTableValueSampler, vec2(m, 0.));
      return unpack(m_value)*255.;

    }

    //
    // Binary Search
    //
    float lookup_id(float key) {
      // Array size
      float first = 0.;
      float last = float(uMergeTableEnd)/float(uMergeTableLength);
      float delta = 1./float(uMergeTableLength);

      // Search within log(n) runtime
      for (int i = 0; i <= bMAX; i++) {
        // Break if list gone
        if (larger(first,last)) break;

        // Evaluate the midpoint
        float mid = (first + last) / 2.0;
        float here = lookup_key_(mid);

        // Search below midpoint
        if (larger(here,key)) last = mid-delta;

        // Search above midpoint
        else if (larger(key,here)) first = mid+delta;

        // Return the key at midpoint
        else return lookup_value_(mid);
      }
      // Assume unmerged
      return key;
    }

    //
    // LOCKTABLE
    //
    bool is_locked(float id) {

      for (int m=0; m<MAX; m++) {

        if (m>uLockTableLength) break;

        vec4 m_key = texture2D(uLockTableSampler, vec2(float(m)/float(uLockTableLength), 0.));
        float m_id = unpack(m_key)*255.;

        // if m_id is larger than our id, then we are done since all entries are sorted
        if (larger(m_id, id)) break;

        if (equals(id, m_id)) {
          // found entry -> label is locked
          return true;
        }

      }

      return false;

    }

    //
    // COLORTABLE
    //
    vec4 lookup_color(float id) {

      float normalized_id = mod(id, uMaxColors);

      // float normalized_id = mod(id, 1.) + 33.; // <---- TESTCASE

      vec2 colormap_pos = vec2(normalized_id / (uMaxColors - 1.), 0.);

      vec4 color = texture2D(uColorMapSampler, colormap_pos);

      // default opacity
      color.a = uOpacity/255.;

      // locked
      if (is_locked(id)) {

        if (mod(vTexturePosition.x, 0.05) < 0.01) {
          color *= 1.5;
          color.a = 0.9;
        }
        else if (mod(vTexturePosition.y, 0.05) < 0.01) {
          color *= 1.5;
          color.a = 0.9;
        } else {
          color.a = 0.5;
        }

      } else {

        if ((uSplitMode > 0) && equals(uActivatedId, id)) {
          // in split mode, the activated id is completely transparent
          return vec4(0.0);
        }

        if ((uAdjustMode > 0) && equals(uActivatedId, id)) {
          // in adjust mode, only show the active label
          return color;
        } else if (uAdjustMode > 0) {
          return vec4(0.0);
        }

        // highlight active labels
        if (equals(uHighlightedId, id) || equals(uActivatedId, id)) {
           color.a = 0.8;
        }

      }

      return color;

    }

    //
    // BORDERS
    //
    bool show_borders(float id) {

      if (uBorders == 1) {

        // grab surrounding ids
        float id_l = unpack(texture2D(uTextureSampler, vec2(vTexturePosition.x - 1.0/512.0, vTexturePosition.y)))*255.;
        id_l = lookup_id(id_l);
        float id_r = unpack(texture2D(uTextureSampler, vec2(vTexturePosition.x + 1.0/512.0, vTexturePosition.y)))*255.;
        id_r = lookup_id(id_r);
        float id_b = unpack(texture2D(uTextureSampler, vec2(vTexturePosition.x, vTexturePosition.y - 1.0/512.0)))*255.;
        id_b = lookup_id(id_b);
        float id_t = unpack(texture2D(uTextureSampler, vec2(vTexturePosition.x, vTexturePosition.y + 1.0/512.0)))*255.;
        id_t = lookup_id(id_t);

        return (!equals(id, id_l) || !equals(id, id_r) || !equals(id, id_b) || !equals(id, id_t));

      }

      return false;

    }

    //
    //
    //
    //
    //
    //
    void main() {

      // grab image data
      vec4 image = texture2D(uImageSampler, vTexturePosition);

      // grab 32 bit segmentation id
      vec4 rgba_ids = texture2D(uTextureSampler, vTexturePosition);
      float id = lookup_id(unpack(rgba_ids)*255.);

      // .. lookup its' color
      vec4 color = lookup_color(id);


      if (show_borders(id)) {

        // show borders in black
        gl_FragColor = vec4(0., 0., 0., 1.);
        return;

      }

      // special rendering case: only locked segments
      if (uOnlyLocked) {
        if (is_locked(id)) {
          color = mix(image, color, color.a);
        } else {
          color = image;
        }

        gl_FragColor = color;
        return;
      }

      if (uShowOverlay) {

        color = mix(image, color, color.a);

      } else {

        color = image;

      }

      gl_FragColor = color;

    }
  </script>

  <script id="vs1" type="x-shader/x-vertex">
    attribute vec3 aPosition;
    attribute vec2 aTexturePosition;

    varying vec3 vPosition;
    varying vec2 vTexturePosition;

    void main() {

      vTexturePosition = aTexturePosition;

      gl_Position = vec4(aPosition,1.);

    }

  </script>

  <script type="text/javascript">

  Opentip.defaultStyle = 'dark';

  window.onload = function() {

    DOJO.init();

  };

  </script>

</head>

<body>

<div id='blocker'>
  <br>
  <br>
  <br>
  <br>
  <p align="center">
  <font size=+5>Saving..</font>
  </p>
</div>

<div id='loading_blocker'>
  <br>
  <br>
  <br>
  <br>
  <p align="center">
  <font size=+5>Loading..</font>
  </p>
  <br>
  <br>
  <center><img src='gfx/loading.jpg'></center>
  <br>
  <br>
</div>

  <div id='dojo1' class='viewer'></div>

  <div id='tools' class='toolbar'>
    <div id='undoredo'>
      <img src='gfx/undo.png' id='undo' class='tool' data-ot='Undo' data-ot-target='true' data-ot-tip-joint='right'>
      <img src='gfx/redo.png' id='redo' class='tool' data-ot='Redo' data-ot-target='true' data-ot-tip-joint='right'>
    </div>
    <img src='gfx/merge.png' id='merge' class='tool' data-ot='Merge Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/merge_selected.png' id='merge_selected' class='tool' style='display:none' data-ot='Stop Merge Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/split.png' id='split' class='tool' data-ot='Split Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/split_selected.png' id='split_selected' class='tool' style='display:none' data-ot='Stop Split Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/3d.png' id='3d' class='tool' style='margin-top:3px;' data-ot='3D Rendering' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/3d_selected.png' id='3d_selected' class='tool' style='margin-top:3px;display:none' data-ot='Stop 3D Rendering' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/save.png' id='save' class='tool' style='margin-top:6px;' data-ot='Save!' data-ot-target='true' data-ot-tip-joint='right'>
  </div>

  <div id='info' class='infopanel'>
    <div><div id='slicenumber'>0/X</div><div id='label'>Label n/a</div></div>
    <div><strong>W/S</strong>: next/previous slice</div>
    <div><strong>C/X</strong>: zoom in/out</div>
    <div><strong>L</strong>: lock/unlock segment</div>
    <div><strong>Q</strong>: toggle segmentation</div>
    <div><strong>A</strong>: toggle borders</div>
    <div><strong>P</strong>: only show locked segments</div>
    <div><strong>F</strong>: toggle borders and segmentation</div>
    <div><strong>E/D</strong>: increase/decrease opacity</div>
    <div><strong>ESC</strong>: discard split or reset tool</div>
    <div><strong>J</strong>: jump to X,Y,Z</div>
    <div><strong>K</strong>: jump to Z</div>
    <div><strong>1-5</strong>: toggle tool</div>
    <div><strong>- / =</strong>: de-/increase brushsize</div>
  </div>

  <div id='log' class='logpanel'>
  </div>

  <div id='threeD' class='draggable_panel' style='left:300px;top:10px;'>
    <div class="drsMoveHandle" style='position:absolute;width:100%;z-index:2000;'>3D Rendering</div>
    <div id='threeDcontent' style='width:100%;height:100%;'></div>
  </div>

  <div id='todo' class='draggable_panel' style='left:300px;top:330px;'>
    <div class="drsMoveHandle noselect">Orphans</div>
    <div id='orphan_left_arrow' class='noselect default_arrow'>&#171;</div>
    <div id='orphan_color' class='noselect'></div>
    <div id='orphan_id'>ID: 000000</div>
    <div id='orphan_unsure' class='noselect orphan_button_off'>?</div>
    <div id='orphan_correct' class='noselect orphan_button_off'>&#10003;</div>
    <div id='orphan_right_arrow' class='noselect default_arrow'>&#187;</div>

  </div>

</body>

</html>
