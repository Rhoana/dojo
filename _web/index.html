<html>
<head>

  <meta name='viewport' content='width=device-width, initial-scale=1.0, minimum-scale=1.0, maximum-scale=1.0' />

  <link rel="stylesheet" type="text/css" href="css/dojo.css">
  <link rel="stylesheet" type="text/css" href="css/opentip.css">
  <link rel="stylesheet" type="text/css" href="css/dragresize.css">

  <script type='text/javascript' src='lib/jquery.min.js'></script>
  <script type='text/javascript' src='lib/jquery-ui.min.js'></script>
  <script type='text/javascript' src='lib/jquery.slimscroll.min.js'></script>
  <script type="text/javascript" src='lib/zlib.min.js'></script>
  <script type="text/javascript" src='lib/xtk.js'></script>
  <script type="text/javascript" src='lib/opentip-native.min.js'></script>
  <script type="text/javascript" src='lib/dragresize.js'></script>

  <script type='text/javascript' src='dojo.js'></script>
  <script type='text/javascript' src='viewer.js'></script>
  <script type='text/javascript' src='controller.js'></script>
  <script type='text/javascript' src='camera.js'></script>
  <script type='text/javascript' src='interactor.js'></script>
  <script type='text/javascript' src='loader.js'></script>
  <script type='text/javascript' src='websocket.js'></script>
  <script type='text/javascript' src='offscreen_renderer.js'></script>
  <script type='text/javascript' src='util.js'></script>


<!--   // <script type="text/javascript" src="lib/X/lib/google-closure-library/closure/goog/base.js"></script>
  // <script type="text/javascript" src="lib/X/xtk-deps.js"></script> -->


  <!-- SHADERS -->
  <script id="fs1" type="x-shader/x-fragment">
    #version 300 es
    precision highp float;
    precision highp int;
    precision highp usampler2D;

    uniform sampler2D uImageSampler;
    uniform sampler2D uColorMapSampler;
    uniform usampler2D uTextureSampler;
    uniform usampler2D uLockTableSampler;
    uniform usampler2D uMergeTableKeySampler;
    uniform usampler2D uMergeTableValueSampler;
    // Values for given IDS
    uniform uint uActivatedId;
    uniform uint uHighlightedId;
    // Display settings
    uniform int uBorders;
    uniform float uOpacity;
    uniform bool uOnlyLocked;
    uniform bool uShowOverlay;
    // Current modes
    uniform int uSplitMode;
    uniform int uAdjustMode;
    // Information about textures
    uniform float uMaxColors;
    uniform float uMergeTableLength;
    uniform float uLockTableLength;

    // Max number of merges or locks
    const int MAX = 10000;
    const int bMAX = int(ceil(log2(float(MAX))));

    in vec3 vPosition;
    in vec2 vTexturePosition;

    out vec4 fragmentColor;

    // Convert int rgba to a 32 bit value
    uint unpack (uvec4 bytes) {
      uint sum = (bytes.a << 24) | (bytes.b << 16);
      sum = sum | (bytes.g << 8) | (bytes.r);
      return sum;
    }

    //
    // Unpack ID from texture
    //
    uint id_map(uint m, usampler2D samp, float len) {
      // Value for given index
      vec2 m_pos = vec2(float(m)/len, 0.0);
      uvec4 m_value = texture(samp, m_pos);
      return unpack(m_value);
    }

    //
    // Binary Search
    //
    bool bin_search(uint key, usampler2D samp, float len) {

      // Array size
      uint first = uint(0);
      uint last = uint(len);

      // Search within log(n) runtime
      for (int i = 0; i <= bMAX; i++) {
        // Break if list gone
        if (first > last) break;

        // Evaluate the midpoint
        uint mid = (first + last) / uint(2);
        uint here = id_map(mid, samp, len);

        // Search below midpoint
        if (key < here){
          last = mid - uint(1);
          continue;    
        }
        // Search above midpoint
        if (key > here){
          first = mid + uint(1);
          continue;
        }
        // Return found
        return true;
      }
      // Not found
      return false;
    }

    //
    // MERGETABLE
    //
    uint merge_id(uint key) {
      if (bin_search(key, uMergeTableKeySampler, uMergeTableLength)){
        // Return value from merge table
        return id_map(key, uMergeTableValueSampler, uMergeTableLength);
      }
      return key;
    }

    //
    // LOCKTABLE
    //
    bool is_locked(uint key) {
      return bin_search(key, uLockTableSampler, uLockTableLength);
    }

    //
    // BORDERS
    //
    bool show_borders(uint id) {

      if (uBorders == 1) {

        // Offset one pixel
        const float delta = 1.0/512.0;

        // grab surrounding ids
        uint id_l = unpack(texture(uTextureSampler, vec2(vTexturePosition.x - delta, vTexturePosition.y)));
        uint id_r = unpack(texture(uTextureSampler, vec2(vTexturePosition.x + delta, vTexturePosition.y)));
        uint id_b = unpack(texture(uTextureSampler, vec2(vTexturePosition.x, vTexturePosition.y - delta)));
        uint id_t = unpack(texture(uTextureSampler, vec2(vTexturePosition.x, vTexturePosition.y + delta)));

        // Use merges
        id_l = merge_id(id_l);
        id_r = merge_id(id_r);
        id_b = merge_id(id_b);
        id_t = merge_id(id_t);

        return (id != id_l || id != id_r || id != id_b || id != id_t);
      }
      return false;
    }

    //
    // COLORTABLE
    //
    vec4 lookup_color(uint id) {
      // Get the color of the ID from colormap
      float mod_id = mod(float(id), uMaxColors);
      vec2 colormap_pos = vec2(mod_id / (uMaxColors - 1.0), 0.);
      vec4 color = texture(uColorMapSampler, colormap_pos);

      // default opacity
      return vec4(color.rgb, uOpacity/255.0);
    }

    //
    // Get color of id
    //
    vec4 id_color(uint id) {

      // Background
      if (id == uint(0)){
        return vec4(0.0);
      }
      // Borders
      if (show_borders(id)){
        return vec4(vec3(0.0), 1.0);
      }

      // Locked ID values
      if (is_locked(id)) {
        // Stripes every 0.05 of size 0.01
        bool xbar = mod(vTexturePosition.x, 0.05) < 0.01;
        bool ybar = mod(vTexturePosition.y, 0.05) < 0.01;
        // Lookup the color
        vec4 color = lookup_color(id);

        if (xbar || ybar) {
          // Highlight
          return vec4(color.rgb * 1.5, color.a);
        }
        // Dim alpha
        return vec4(color.rgb, color.a * 0.5);
      }

      // Hide all unlocked
      if (uOnlyLocked){
        return vec4(0.0);
      }

      if (uSplitMode > 0) {
        // Hide the active label
        if (uActivatedId == id){
          return vec4(0.0);
        }
        // Lookup the color
        return lookup_color(id);
      }

      if (uAdjustMode > 0) {
        // Only show the active label
        if (uActivatedId == id){
          // Lookup the color
          return lookup_color(id);
        }
        return vec4(0.0);
      }

      // Lookup the color
      vec4 color = lookup_color(id);

      if (uHighlightedId == id) {
        return vec4(color.rgb, 0.8);
      }

      if (uActivatedId == id) {
        return vec4(color.rgb, 0.8);
      }

      // Normal Id
      return color;
    }


    //
    //
    //
    //
    //
    //
    void main() {

      // grab image data
      vec4 image = texture(uImageSampler, vTexturePosition);
      // Simple with no ids
      if (!uShowOverlay){
        fragmentColor = image;
        return;
      }

      // grab 32 bit segmentation ID
      uvec4 rgba_ids = texture(uTextureSampler, vTexturePosition);
      uint id = merge_id(unpack(rgba_ids));
      // Color Segmentation ID
      vec4 color = id_color(id);
      // Render Color over image
      fragmentColor = mix(image, color, color.a);
      return;
    }
  </script>

  <script id="vs1" type="x-shader/x-vertex">
    #version 300 es
    in vec3 aPosition;
    in vec2 aTexturePosition;

    out vec3 vPosition;
    out vec2 vTexturePosition;

    void main() {

      vTexturePosition = aTexturePosition;

      gl_Position = vec4(aPosition,1.);

    }

  </script>

  <script type="text/javascript">

  Opentip.defaultStyle = 'dark';

  window.onload = function() {

    DOJO.init();

  };

  </script>

</head>

<body>

<div id='blocker'>
  <br>
  <br>
  <br>
  <br>
  <p align="center">
  <font size=+5>Saving..</font>
  </p>
</div>

<div id='loading_blocker'>
  <br>
  <br>
  <br>
  <br>
  <p align="center">
  <font size=+5>Loading..</font>
  </p>
  <br>
  <br>
  <center><img src='gfx/loading.jpg'></center>
  <br>
  <br>
</div>

  <div id='dojo1' class='viewer'></div>

  <div id='tools' class='toolbar'>
    <div id='undoredo'>
      <img src='gfx/undo.png' id='undo' class='tool' data-ot='Undo' data-ot-target='true' data-ot-tip-joint='right'>
      <img src='gfx/redo.png' id='redo' class='tool' data-ot='Redo' data-ot-target='true' data-ot-tip-joint='right'>
    </div>
    <img src='gfx/merge.png' id='merge' class='tool' data-ot='Merge Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/merge_selected.png' id='merge_selected' class='tool' style='display:none' data-ot='Stop Merge Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/split.png' id='split' class='tool' data-ot='Split Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/split_selected.png' id='split_selected' class='tool' style='display:none' data-ot='Stop Split Mode' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/3d.png' id='3d' class='tool' style='margin-top:3px;' data-ot='3D Rendering' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/3d_selected.png' id='3d_selected' class='tool' style='margin-top:3px;display:none' data-ot='Stop 3D Rendering' data-ot-target='true' data-ot-tip-joint='right'>
    <img src='gfx/save.png' id='save' class='tool' style='margin-top:6px;' data-ot='Save!' data-ot-target='true' data-ot-tip-joint='right'>
  </div>

  <div id='info' class='infopanel'>
    <div><div id='slicenumber'>0/X</div><div id='label'>Label n/a</div></div>
    <div><strong>W/S</strong>: next/previous slice</div>
    <div><strong>C/X</strong>: zoom in/out</div>
    <div><strong>DEL</strong>: delete segment</div>
    <div><strong>L</strong>: lock/unlock segment</div>
    <div><strong>Q</strong>: toggle segmentation</div>
    <div><strong>A</strong>: toggle borders</div>
    <div><strong>P</strong>: only show locked segments</div>
    <div><strong>F</strong>: toggle borders and segmentation</div>
    <div><strong>E/D</strong>: increase/decrease opacity</div>
    <div><strong>ESC</strong>: discard split or reset tool</div>
    <div><strong>J</strong>: jump to X,Y,Z</div>
    <div><strong>K</strong>: jump to Z</div>
    <div><strong>1-5</strong>: toggle tool</div>
    <div><strong>- / =</strong>: de-/increase brushsize</div>
  </div>

  <div id='log' class='logpanel'>
  </div>

  <div id='threeD' class='draggable_panel' style='left:300px;top:10px;'>
    <div class="drsMoveHandle" style='position:absolute;width:100%;z-index:2000;'>3D Rendering</div>
    <div id='threeDcontent' style='width:100%;height:100%;'></div>
  </div>

</body>

</html>
